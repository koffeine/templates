plugins {
	id 'org.springframework.boot' version '4.0.3'
	id 'com.github.ben-manes.versions' version '0.53.0'
	id 'java'
	id 'idea'
}

apply plugin: 'io.spring.dependency-management'

repositories {
	mavenCentral()
}

dependencies {
	annotationProcessor(
		'org.springframework.boot:spring-boot-configuration-processor'
	)

	implementation(
		'org.springframework.boot:spring-boot-starter-data-jpa',
		'org.springframework.boot:spring-boot-starter-validation',
		'org.springframework.boot:spring-boot-starter-webmvc'
	)

	runtimeOnly(
		'com.h2database:h2'
	)

	testImplementation(
		'org.springframework.boot:spring-boot-starter-data-jpa-test',
		'org.springframework.boot:spring-boot-starter-validation-test',
		'org.springframework.boot:spring-boot-starter-webmvc-test'
	)

	testRuntimeOnly(
		'org.junit.platform:junit-platform-launcher'
	)
}

java {
	sourceCompatibility = JavaVersion.VERSION_25
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.compilerArgs = [ '-Xlint:deprecation', '-Xlint:unchecked', '-parameters' ]
}

jar.enabled = false

test {
	useJUnitPlatform()
}


dependencyUpdates {
	gradleReleaseChannel = 'current'
	rejectVersionIf { version ->
		[ 'rc', 'm' ].any { qualifier ->
			version.candidate.version ==~ /(?i).*[\.-]${qualifier}[\d]*/
		}
	}
}


idea {
	module {
		excludeDirs += files('.idea', 'gradle')
	}
}


wrapper {
	gradleVersion = '9.3.1'
}
